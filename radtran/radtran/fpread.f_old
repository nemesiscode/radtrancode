      subroutine fpread(vv,temp,kpara,absh2,abshe,dah2dT,dahedT,
     1 deltaf)
C     **********************************************************************
C     Reads in the collision-induced absorption coefficients of molecular
C     hydrogen and helium for a range of different para-H2 fractions.
C
C     Units of absorption : cm-1
C
C     input parameters:
C     
C	vv            	real	is the wavenumber wanted (may not
C                              	be an exact value on the ciatable,
C                              	hence interpolate.
C
C     	temp         	real	the temperature, also may require
C                        	interpolation
C
C
C     Output Parameters 
C
C	absh2(NPARA)	real	H2-H2 CIA absorption for different para-H2
C				fractions
C	abshe(NPARA)	real	H2-He CIA absorption for different para-H2
C				fractions
C	dah2dT(NPARA)	real	Rate of change of H2-H2 absorption with
C				temperature
C	dahedT(NPARA)	real	Rate of change of H2-He absorption with
C				temperature
C	deltaf		real	Step in para-H2 table
C
C     P.Irwin	26/7/01
C	Fletcher (07/10/10) Removed explicit reference to 12 para fractions,
C				Changed to NPARA (also needs to be modified in 
C				nparacon.f and ciatable/fparatable.f
c     Fletcher (31/01/11) Added parameter DF to subroutine fpread.f

C     **********************************************************************

      implicit none
      include '../includes/ciacom.f'

      real dnu,deltaf

C     input/output variables
      real vv,temp,absh2(NPARA),abshe(NPARA),dah2dT(NPARA),dahedT(NPARA)

C     Other variables
      real temps(NUMT)
      real kh2h2(NPARA,NUMT,NUMWN),vvs(NUMWN)
      real kh2he(NPARA,NUMT,NUMWN),fp(NPARA),fp1(12)

      real abs11, abs12, abs21, abs22
      real bbs11, bbs12, bbs21, bbs22
      real u,t,temp1,dudt
      integer cv,ct,pindex,i,it1,it2,iv,ipara
      integer kpara,iread
      character*60 ipfile
      character*80 aname

C     ----------------------------------------------------------------------
      common /fptable/ iread,aname,dnu,vvs,temps,ipara,fp,kh2h2,kh2he,
     &  it1, it2, iv
C     ----------------------------------------------------------------------

C     define the fixed parameters

      if(iread.eq.1)then

        call datarchive(aname)

        open(12,file=aname,form='unformatted',status='old')      
         print*,'FPREAD. Opening look-up file'
         print*,'File =',aname
         print*,'Tabulated temperatures : '
         read(12)temps
         do 21 i=1, NUMT
            print*,i,temps(i)
21       continue
         print*,'Tabulated para-H2 fractions : '
         if(ipara.eq.12)then
          read(12)fp1
          do i=1,ipara
           fp(i)=fp1(i)
          enddo
         else
          read(12)fp
         endif
         kpara=ipara

         do 22 i=1, ipara
            print*,i,fp(i)
22       continue

         read(12)kh2h2
         read(12)kh2he
         close(12)

C     ----------------------------------------------------------------------
C     initialisation section
C     ----------------------------------------------------------------------

C        set up the wavenumber array
         do 23 i=1,NUMWN
            vvs(i) = (i-1)*DNU
23       continue

C        initialise the absorption array to be zero in case of failure
         do 24 i=1, IPARA
            absh2(i) = 0.0
            abshe(i) = 0.0
 24      continue

         print*,'fpread : New Data read in'

      end if

C     ----------------------------------------------------------------------
C     range checking
C     ----------------------------------------------------------------------

      if(vv.gt.vvs(NUMWN).or.vv.lt.vvs(1).and.iv.ne.1)then
         print*,'fpread : wavenumber out of range'
         iv=1
         return
      end if

      if(temp.gt.temps(NUMT).and.it1.ne.1)then
         print*,'fpread: warning temp > MaxT ',temp
         print*,'Setting to ',temps(NUMT)
         temp1 = temps(NUMT)
         it1=1
      else
         temp1 = temp
      end if

      if(temp.lt.temps(1).and.it2.ne.1)then
         print*,'fpread: warning temp <  MinT ',temp
         print*,'Setting to ',temps(1)
         temp1 = temps(1)
         it2=1
      else
         temp1 = temp
      end if

C     ----------------------------------------------------------------------
C     main section
C     ----------------------------------------------------------------------


      deltaf=fp(2)-fp(1)


C     go away and find the plave in the table for wavenumber:
      i = NUMWN
      cv = pindex(vvs,i,vv)

C     ... and for temperature:
      i = NUMT
      ct = pindex(temps,i,temp1)

C     The returned values are the indices of the array immediately
C     below or equal to the required temp or wavenumber. These indices
C     are the same, of course, for both H2-H2 and H2-He gas pairs.

      t = (vv - vvs(cv))/(vvs(cv+1)-vvs(cv))
      u = (temp1 - temps(ct))/(temps(ct+1)-temps(ct))
      dudt = 1.0/(temps(ct+1)-temps(ct))


C     Loop over all tabulated para-H2 fractions
      do 26 i=1,IPARA
         abs11 = kh2h2(i,ct,cv)
         abs12 = kh2h2(i,ct,cv+1)
         abs22 = kh2h2(i,ct+1,cv+1)
         abs21 = kh2h2(i,ct+1,cv)
         bbs11 = kh2he(i,ct,cv)
         bbs12 = kh2he(i,ct,cv+1)
         bbs22 = kh2he(i,ct+1,cv+1)
         bbs21 = kh2he(i,ct+1,cv)
 
         absh2(i) = (1.0-t)*(1.0-u)*abs11 + t*(1.0-u)*abs12 + 
     &        t*u*abs22 + (1.0-t)*u*abs21
         abshe(i) = (1.0-t)*(1.0-u)*bbs11 + t*(1.0-u)*bbs12 + 
     &        t*u*bbs22 + (1.0-t)*u*bbs21
	 dah2dT(i) = -(1.0-t)*abs11 -t*abs12 + t*abs22 + (1.0-t)*abs21
	 dahedT(i) = -(1.0-t)*bbs11 -t*bbs12 + t*bbs22 + (1.0-t)*bbs21

	 dah2dt(i)=dah2dt(i)*dudt
	 dahedt(i)=dahedt(i)*dudt

26    continue

      return

      end

