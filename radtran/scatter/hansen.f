      SUBROUTINE HANSEN( IC, PPL, PMI, MAXP, WTMU, NMU)
C
C  NORMALIZE THE SCATTERING PHASE FCN., USING METHOD OF HANSEN (1971,
C  J.ATM.SCI., V28, 1400)
C
C  PPL,PMI ARE THE FORWARD AND BACKWARD PARTS OF THE AZIMUTHALLY-INTEGRATED
C  PHASE FUNCTION.  THE NORMALIZATION OF THE TRUE PHASE FCN. IS:
C    integral over sphere [ P(mu,mu',phi) * dO] = 1
C  WHERE dO IS THE ELEMENT OF SOLID ANGLE AND phi IS THE AZIMUTHAL ANGLE.
C
C  23JAN89 ... CODE EXTRACTED FROM PREVIOUS SUBR. NPHASE (RENAMED PHASINT)
C   7FEB89 ... ADDED FACTOR OF 2*PI:  NORMALIZE TO 1.0.

      IMPLICIT NONE
      INTEGER NMU, I, J, IC, MAXP, NITER
      INCLUDE '../includes/arrdef.f'
      DOUBLE PRECISION FC(MAXMU,MAXMU), RSUM(MAXMU), TSUM(MAXMU), 
     1 PPL(MAXP,MAXP), PMI(MAXP,MAXP), PPL0(MAXMU,MAXMU), 
     2 MU(MAXMU), WTMU(MAXMU), XJ, XI, TEST, TESTJ, PI, X1
C

      COMMON /HANSBL/ FC

      PI = 4.0D0*DATAN(1.0D0)
      X1 = 2.0D0*PI

      IF (NMU.GT.MAXMU .OR. NMU.GT.MAXP) CALL ABEND(' TOO MANY ANGLES')
C
      IF(IC.EQ.0)THEN

       DO J = 1,NMU
	DO I = 1,NMU
	  FC(I,J) = 1.0D0
	ENDDO
       ENDDO
C
       DO J = 1,NMU
	RSUM(J) = 0.0D0
	DO I = 1,NMU
	  RSUM(J) = RSUM(J)+PMI(I,J)*WTMU(I)*X1
	ENDDO
       ENDDO
       NITER = 0
110    NITER = NITER+1
       IF (NITER.GT.2000) THEN
         PRINT*,'SUBR. HANSEN:  NORMALIZATION FAILS TO CONVERGE'
         PRINT*,'Leaving phase matrices uncorrected' 
         RETURN
       ENDIF
       TEST = 0.0D0
       DO J = 1,NMU
	TSUM(J) = 0.0D0
	DO I = 1,NMU
	  TSUM(J) = TSUM(J)+PPL(I,J)*WTMU(I)*FC(I,J)*X1
	ENDDO
	TESTJ = ABS(RSUM(J)+TSUM(J)-1.D0)
	IF(TESTJ.GT.TEST) TEST = TESTJ
       ENDDO
       IF (TEST.LT.1.0D-14) GO TO 120
       DO J = 1,NMU
	XJ = (1.D0-RSUM(J))/TSUM(J)
	DO I = 1,J
	  XI = (1.D0-RSUM(I))/TSUM(I)
	  FC(I,J) = 0.5D0*(FC(I,J)*XJ+FC(J,I)*XI)
	  FC(J,I) = FC(I,J)
	ENDDO
       ENDDO
       GO TO 110

120    CONTINUE

      ENDIF
      DO J = 1,NMU
	DO I = 1,NMU
	  PPL(I,J) = PPL(I,J)*FC(I,J)
	ENDDO
      ENDDO

      RETURN
      END
